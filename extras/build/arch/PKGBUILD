# Maintainer: Patrick Wu <me@patrickwu.space>

pkgname=wslu
pkgver=4.1.0
pkgrel=0
pkgdesc="A collection of utilities for the Windows Subsystem for Linux"
arch=('any')
url='https://github.com/wslutilities/wslu'
license=('GPL-3.0-or-later')
depends=('bc' 'bash-completion' 'psmisc')
optdepends=('imagemagick: custom icon support')
source=("git+https://github.com/wslutilities/wslu.git#tag=v${pkgver}")
sha256sums=('SKIP')

build() {
  cd wslu
  bash ./configure.sh --build
  make
}

package() {
  cd wslu
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/wslu/
}

# vim: ts=2 sw=2 et:
